plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'kotlin-android'
}

group = 'com.github.highcharts'
version = '11.4.8'

ext{
    publishedGroupId = 'com.highsoft.highcharts'
    libraryName = 'Highcharts'
    artifact = 'highcharts'

    libraryDescription = 'Highcharts Android is a delightful wrapper of HighchartsJS for Android.'

    siteUrl = 'https://www.highcharts.com/'
    gitUrl = 'https://github.com/highcharts/highcharts-android'

    libraryVersion = '11.4.8'

    developerId = 'bgn'
    developerName = 'Bartosz Gnacek'
    developerEmail = 'bartosz.gnacek@spinney.io'

    licenseName = 'Custom'
    licenseUrl = 'https://github.com/highcharts/highcharts-android/blob/master/LICENSE'
}

repositories {
    mavenCentral()
    google()
    maven { url "https://jitpack.io" }
}

android {
    defaultConfig {
        compileSdk 30
    }

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 1
        versionName '11.4.8'

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

}

tasks.register('javadoc', Javadoc) {
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    android.libraryVariants.configureEach { variant ->
        if (variant.name == 'release') {
            owner.classpath += variant.javaCompileProvider.get().classpath
        }
    }
    options.memberLevel = JavadocMemberLevel.PUBLIC

    options.addStringOption('Xdoclint:none', '-quiet')
    options.addStringOption('nonavbar')
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.test.ext:junit:1.0.0'
    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    androidTestImplementation('androidx.test:rules:1.4.0')
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'com.google.android.material:material:1.2.0'
    implementation 'com.google.code.gson:gson:2.10'

    testImplementation 'junit:junit:4.12'
    testImplementation "org.mockito:mockito-core:4.0.0"
    testImplementation "org.mockito.kotlin:mockito-kotlin:4.0.0"
}

afterEvaluate {
    assemble.dependsOn test
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId = publishedGroupId
                artifactId = artifact
                version = libraryVersion

                pom {
                    name = libraryName
                    description = libraryDescription
                    url = siteUrl

                    licenses {
                        license {
                            name = licenseName
                            url = licenseUrl
                        }
                    }
                    developers {
                        developer {
                            id = developerId
                            name = developerName
                            email = developerEmail
                        }
                    }
                    scm {
                        connection = gitUrl
                        developerConnection = gitUrl
                        url = siteUrl
                    }
                }
            }
        }
    }
}

